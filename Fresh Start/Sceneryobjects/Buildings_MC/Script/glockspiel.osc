'######################
'Clock with Carillon
'######################

'Carillon with Clock with Minute and Hour Hand

'(c) 11.04.2012 Marcel Kuhnt

'Script Version: 1.2
'Omsi release: 1.0

'Needs:
'- none




{init}
	0 (S.L.clock_bell_used)
	0 (S.L.clock_bell_quarters_left)
	0 (S.L.clock_bell_hours_left)
	0 (S.L.clock_bell_melodie_left)
	0 (S.L.clock_bell_used)
	0 (S.L.clock_bell_timer)

	0 (S.L.clock_bell_quarters_cnt)
{end}

{frame}

'###########################
' Clock Hands
'###########################

'Time in seconds...

(L.S.Time) s0
'Hour Hand: 360° in 12 hours:
		120 / (S.L.clock_hand_hour)
'Minute Hand: 360° in 1 hour:
		l0 10 / (S.L.clock_hand_minute)


'###########################
' Clock Bell
'###########################

'Ermitteln der aktuellen Viertelstunde:
	(L.S.Time) 900 / s0 trunc s1
'L0 = Zeit in Viertelstunden (float), L1 = Anzahl der abgeschlossenen Viertelstunden
'Bestimmen, ob geschlagen werden kann:
'Innerhalb der ersten drei Minuten nach dem Erreichen einer vollen Viertelstunde:
	l0 l1 - 0.2 <
	{if}
'Wenn noch nicht fertig geschlagen wurde, mach weiter!
		(L.L.clock_bell_used) !
		{if}
			(L.L.clock_bell_quarters_left) 0 =
			(L.L.clock_bell_hours_left) 0 = &&
			(L.L.clock_bell_melodie_left) 0 = &&
'Läutwerk wird auf neuen Schlagablauf eingestellt:
			{if}
				l1 4 / trunc s2
'S2 = Anzahl der abgeschlossenen Stunden
				l1 l2 4 * - s3
'S3 = Anzahl der überzähligen Viertelstunden. Falls = 0 mach 4 draus:
				0 = {if} 4 s3 {endif}
'Zurücksetzen des Zählers für unterschiedliche Viertelstundenzähler
				0 (S.L.clock_bell_quarters_cnt)
'Abspeichern als Anzahl der noch abzuspielenden Viertelschläge:
				l3 (S.L.clock_bell_quarters_left)

				4 =
				{if}
					l2 12 =
					{if}
'Aktivieren der Melodie, wenn 12 Uhr:
						1 (S.L.clock_bell_melodie_left)
					{endif}

'Einstellen der abzuspielenden Stundenschläge - bereinigen auf 1-12 (nur wenn 4 Viertelschläge):
					l2 12 > {if} l2 12 - s2 {endif}
					l2 0 = {if} 12 s2 {endif}
					l2 (S.L.clock_bell_hours_left)
				{endif}
			{endif}
		{endif}
	{else}
		0 (S.L.clock_bell_used)
	{endif}

'Nun entsprechend abspielen:

	(L.L.clock_bell_quarters_left) 0 >
	(L.L.clock_bell_hours_left) 0 >	||
	(L.L.clock_bell_melodie_left) 0 > ||
	(L.L.clock_bell_used) ! &&
	{if}
		(L.L.clock_bell_timer) s0 0 >
		{if}
			l0 (L.S.Timegap) - (S.L.clock_bell_timer)
		{else}
			(L.L.clock_bell_quarters_left) 0 >
			{if}
				(L.L.clock_bell_quarters_cnt) 1 + (S.L.clock_bell_quarters_cnt)
				"glocksp_q" $IntToStr ".wav" $+ $+
				(T.F.clock_bell)
				(L.L.clock_bell_quarters_left) 1 -
				(S.L.clock_bell_quarters_left)
				0 >
				{if}
					3.0 (S.L.clock_bell_timer)
				{else}
					4.5 (S.L.clock_bell_timer)
				{endif}
			{else}
				(L.L.clock_bell_hours_left) 0 >
				{if}
					"glocksp_h.wav"
					(T.F.clock_bell)
					(L.L.clock_bell_hours_left) 1 -
					(S.L.clock_bell_hours_left)
					0 >
					{if}
						3.0 (S.L.clock_bell_timer)
					{else}
						5.0 (S.L.clock_bell_timer)
					{endif}
				{else}
					"glocksp_melodie.wav"
					(T.F.clock_bell)
					0 (S.L.clock_bell_melodie_left)
				{endif}
			{endif}
		{endif}
		
		(L.L.clock_bell_quarters_left) 0 =
		(L.L.clock_bell_hours_left) 0 = &&
		(L.L.clock_bell_melodie_left) 0 = &&
		{if}
			1 (S.L.clock_bell_used)
			0 (S.L.clock_bell_timer)
		{endif}

	{endif}



'###########################
' Digital Clock
'###########################

'Was soll er machen? Zeit oder Temp?
	(L.S.Time) 40 % 20 <
	{if}
'Stunden:
		(L.S.Time) 3600 / s0 trunc s1 "02" $IntToStrEnh ":" $+
'Minuten:
		l0 l1 - 60 * trunc "02" $IntToStrEnh $+
	{else}
		(L.S.Weather_Temperature) s0
		0 >=
		{if}
			"+"
		{else}
			"-"
		{endif}
		l0 abs " 2" $IntToStrEnh $+ "°" $+
	{endif}
	(S.$.digitalclock)

'Omsi sagen, dass die Strings-Ausgaben aktualisiert werden sollen:
	1 (S.L.Refresh_Strings)


{end}