'######################
'U Ruhleben
'######################

'Rolltreppe und Anschlusslampe

'(c) 7.7.2011 Marcel Kuhnt

'Script Version: 1.0
'Omsi release: 1.0

'Needs:
'- none




{init}
	0 (S.L.rolltreppe)
	0 (S.L.anschlusslampe_winkel)
{end}

{frame}

'###########################
' Rolltreppe
'###########################

	(L.L.NightlightA)
	{if}	
		0 (S.L.rolltreppe)
	{else}
		(L.S.Timegap) -0.05 * (L.L.rolltreppe) + (S.L.rolltreppe)

		(L.L.rolltreppe) 0 <
		{if}
			(L.L.rolltreppe) 1 + (S.L.rolltreppe)
		{endif}

	{endif}

'###########################
' Anschlussleuchte
'###########################

'Einschaltbedingungen: Wenn vor 00:49 oder nach 04:04 und zwischen Minute 01 und 02:30 plus n*5min:
	(L.S.Time) s0 2940 <
	l0 14640 > ||
	l0 300 % 60 > &&
	l0 300 % 150 < &&
	(S.L.anschlusslampe_on) s0

	l0
	{if}	
		(L.S.Timegap) 2.0 * (L.L.anschlusslampe_winkel) + (S.L.anschlusslampe_winkel)

		(L.L.anschlusslampe_winkel) 1 >
		{if}
			(L.L.anschlusslampe_winkel) 1 - (S.L.anschlusslampe_winkel)
		{endif}
	{endif}

'###########################
' Clock Hands
'###########################

'Time in seconds...

(L.S.Time) s0
'Hour Hand: 360° in 12 hours:
		120 / (S.L.clock_hand_hour)
'Minute Hand: 360° in 1 hour:
		l0 10 / (S.L.clock_hand_minute)
'Second Hand: 360° in 1 minute:
'		l0 trunc 6 * (S.L.clock_hand_next) s1

'Ermitteln der nächsten Abfahrtszeit:
	l0 60 / s1 4 + 5 % l1 - /-/ 5 + s2
	l2 190 >
	{if}
		l2 246 max s2
		1 s4
	{else}
		l2 51 < s4
	{endif}
	l2 60 % 6 * (S.L.clock_hand_next) s1

'Second Hand red or yellow:
	l4
	{if}
		l1 6 / 5 % 0 = ! 1 + (S.L.clock_hand_next_RY)
	{else}
		0 (S.L.clock_hand_next_RY)
	{endif}



	(L.S.Time) s0 2940 <
	l0 14640 > ||

{end}